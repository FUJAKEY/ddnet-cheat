if(NOT CMAKE_CROSSCOMPILING)
  find_package(PkgConfig QUIET)
  pkg_check_modules(PC_LIBSODIUM libsodium)
endif()

set_extra_dirs_lib(LIBSODIUM libsodium)
find_library(LIBSODIUM_LIBRARY
  NAMES libsodium libsodium.so
  HINTS ${HINTS_LIBSODIUM_LIBDIR} ${PC_LIBSODIUM_LIBDIR} ${PC_LIBSODIUM_LIBRARY_DIRS}
  PATHS ${PATHS_LIBSODIUM_LIBDIR}
  ${CROSSCOMPILING_NO_CMAKE_SYSTEM_PATH}
)
set_extra_dirs_include(LIBSODIUM libsodium "${LIBSODIUM_LIBRARY}")
find_path(LIBSODIUM_INCLUDEDIR sodium.h 
  PATH_SUFFIXES sodium 
  HINTS ${HINTS_LIBSODIUM_INCLUDEDIR} ${PC_LIBSODIUM_INCLUDEDIR} ${PC_LIBSODIUM_INCLUDE_DIRS}
  PATHS ${PATHS_LIBSODIUM_INCLUDEDIR}
  ${CROSSCOMPILING_NO_CMAKE_SYSTEM_PATH}
)

include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(LibSodium DEFAULT_MSG LIBSODIUM_INCLUDEDIR)

mark_as_advanced(LIBSODIUM_INCLUDEDIR LIBSODIUM_LIBRARY)

if(LIBSODIUM_FOUND)
  set(LIBSODIUM_INCLUDE_DIRS ${LIBSODIUM_INCLUDEDIR})
  if(LIBSODIUM_LIBRARY)
    set(LIBSODIUM_LIBRARIES ${LIBSODIUM_LIBRARY})
  else()
    set(LIBSODIUM_LIBRARIES)
  endif()
endif()
